#library "NMSCRIPT"
#include "zcommon.acs"
//==============================================================================
#define INT_MAX		2147483647
//==============================================================================
int Music_priority = 0;
int MusicVolume = 1.0;
int MusicNumPlaying = -1;
int MapMusicChangerPulse = 0;
int MusicChangerPulse = 0;
//==============================================================================
function int GetAndSetUniqueTID(void)
{
	int tid = ActivatorTID();
	if(tid == 0)
	{
		tid = UniqueTID(0,0);
		Thing_ChangeTID(0,tid);
	}
	else if(ThingCount(T_NONE,tid) > 1)
	{
		tid = UniqueTID(0,0);
		Thing_ChangeTID(0,tid);
	}
	else
	{
		Thing_ChangeTID(0,0);
		if(!IsTIDUsed(tid))
		{
			Thing_ChangeTID(0,tid);
		}
		else
		{
			tid = UniqueTID(0,0);
			Thing_ChangeTID(0,tid);
		}
	}
	return tid;
}

function bool ActorIsMonster(int tid)
{
	return ((ClassifyActor(tid) & ACTOR_MONSTER) == ACTOR_MONSTER);
}

function bool ActorIsAlive(int tid)
{
	return ((ClassifyActor(tid) & ACTOR_ALIVE) == ACTOR_ALIVE);
}

function bool Extra_CheckMaxHealth (void)
{
	if(GetActorProperty(0,APROP_SpawnHealth) < 100)SetActorProperty(0,APROP_SpawnHealth,100);
	return (GetActorProperty(0,APROP_Health) < GetActorProperty(0,APROP_SpawnHealth));
}
//==============================================================================
script "Extra_SelectItemSpawn" (void)
{
	if(StrICmp(Strparam(l:"Item412026"),"0") == 0)
	{
		SetActorState(0,"Include");
	}
	else
	{
		SetActorState(0,"External");
	}
}

script "Extra_GetTID" (void)
{
	int tid = UniqueTID(0,0);
	SetUserVariable(0,"user_tid",tid);
	SetResultValue(tid);
}

script "Extra_SetupItem" (void)
{
	str name;
	if(StrICmp(Strparam(l:"Item412026"),"0") == 0)
	{
		name = Strparam(l:"EXTRA_SENTRYGUNBOX_NAME");
	}
	else
	{
		name = Strparam(l:"EXTERNAL_SENTRYGUNBOX_NAME");
	}
	int tid = GetUserVariable(0,"user_tid");
	if((tid != 0) && !(ClassifyActor(tid) & ACTOR_MONSTER) && !(ClassifyActor(tid) & ACTOR_PLAYER) && CheckActorClass(tid,name))
	{
		GiveActorInventory(tid,"ExtraResetFlagDROPPED",1);
	}
}

script "ExtraBFG_Annihilate" (void)
{
	if((SetActivator(0,AAPTR_TRACER) == 1) && (ClassifyActor(0) & ACTOR_MONSTER) && (ClassifyActor(0) & ACTOR_ALIVE) && !CheckFlag(0,"NODAMAGE") && !((CheckFlag(0,"BOSS") && CheckFlag(0,"NOFEAR") && CheckFlag(0,"NOICEDEATH") && CheckFlag(0,"NOCLIP") && CheckFlag(0,"NOTIMEFREEZE")) || (CheckFlag(0,"BOSS") && CheckFlag(0,"NOFEAR") && CheckFlag(0,"NOICEDEATH") && CheckFlag(0,"GHOST") && CheckFlag(0,"NOTIMEFREEZE"))) && (GetActorProperty(0,APROP_DamageFactor) > 0.01) && !CheckActorClass(0,"WolfensteinSS"))
	{
		SetActorProperty(0,APROP_Invulnerable,0);
		Thing_Damage(0,10000000);
		if(GetActorProperty(0,APROP_HEALTH) > 0)
		{
			SetActorProperty(0,APROP_HEALTH,0);
			Thing_Damage(0,10000000);
		}
		if(ActorIsMonster(0))
		{
			int counter = 0;
			while((counter < 525) && CheckFlag(0,"SOLID"))
			{
				counter++;
				delay(1);
			}
			counter = 0;
			while((counter < 10) && ActorIsMonster(0))
			{
				GiveInventory("ExtraAnnihilateFadeOut",1);
				counter++;
				delay(5);
			}
			if(ActorIsMonster(0))
			{
				StopSound(0,CHAN_WEAPON);
				StopSound(0,CHAN_VOICE);
				StopSound(0,CHAN_ITEM);
				StopSound(0,CHAN_BODY);
				StopSound(0,5);
				StopSound(0,6);
				StopSound(0,7);
				SetActorState(0,"Null");
			}
		}
	}
}

script "Extra_FaceToTarget" (void)
{
	int shooterTID = GetAndSetUniqueTID();
	int shooterX = GetActorX(0);
	int shooterY = GetActorY(0);
	int shooterZ = GetActorZ(0)+(GetActorProperty(0,APROP_Height)/2);
	if(SetActivator(0,AAPTR_TARGET) == 1)
	{
		int targetX = GetActorX(0);
		int targetY = GetActorY(0);
		int targetZ = GetActorZ(0)+(GetActorProperty(0,APROP_Height)/2);
		int vx = targetX-shooterX;
		int vy = targetY-shooterY;
		int vz = targetZ-shooterZ;
		SetActorPitch(shooterTID,VectorAngle(VectorLength(vx,vy),vz));
		SetActorAngle(shooterTID,VectorAngle(vx,vy));
	}
}

script "Extra_CheckActorMonster" (void)
{
	if(ActorIsMonster(0))
	{
		SetResultValue(1);//monster
	}
	else
	{
		SetResultValue(0);
	}
}

Script "Extra_MedicalBackpack" (void)
{
	bool use = false;
	while(ActorIsAlive(0) && Extra_CheckMaxHealth() && (CheckInventory("HealthBackpack") > 0))
	{
		if(!use)
		{
			use = true;
			PlaySound(0,"MedicalBackpack/Use",CHAN_ITEM);
		}
		TakeInventory("HealthBackpack", 1);
		Healthing(1);
		delay(1);
	}
}
//==============================================================================
//Music Changer
script "Extra_CoolDownMusicChanger" (void)
{
	delay(1);
	if(MapMusicChangerPulse > 0)MapMusicChangerPulse--;
	if(MusicChangerPulse > 0)MusicChangerPulse--;
	restart;
}

Script "Extra_PulseMusicChanger" (void)
{
	if(MapMusicChangerPulse != 350)
	{
		MapMusicChangerPulse = 350;//10 секунд.
	}
}

Script "Extra_CheckMusicChanger" (void)
{
	if(MapMusicChangerPulse < 1)
	{
		SetUserVariable(0,"user_music",0);
	}
}

Script "Extra_StartMusic" (int mNum, int priority)
{
	if(ActorIsAlive(0) && (CheckInventory("ExtraMusicScriptRun") < 1))
	{
		GiveInventory("ExtraMusicScriptRun",random(1,INT_MAX));
		int CheckNum = CheckInventory("ExtraMusicScriptRun");
		if(mNum >= 0)
		{
			while(ActorIsAlive(0) && (CheckInventory("ExtraMusicScriptRun") == CheckNum))//Ожидание старта.
			{
				if(MusicChangerPulse < 1)
				{
					Music_priority = 0;
				}
				if(ActorIsAlive(0) && (GetUservariable(0,"user_music") == 0) && (Music_priority < priority))
				{
					SetUserVariable(0,"user_music",1);
					Music_priority = priority;
					priority++;
					//Цикл уменьшения громкости предыдущего трека.
					while((MusicNumPlaying != mNum) && (MusicVolume >= 0.015625) && ActorIsAlive(0) && (GetUservariable(0,"user_music") == 1) && (Music_priority < priority) && (CheckInventory("ExtraMusicScriptRun") == CheckNum))
					{
						if(MusicChangerPulse != 350)
						{
							MusicChangerPulse = 350;//10 секунд.
						}
						MusicVolume -= 0.015625;
						ACS_NamedExecuteWithResult("Extra_VolumeMusic",MusicVolume);
						delay(1);
					}
					//Цикл форсирования трека.
					while(ActorIsAlive(0) && (GetUservariable(0,"user_music") == 1) && (Music_priority < priority) && (CheckInventory("ExtraMusicScriptRun") == CheckNum))
					{
						if(MusicChangerPulse != 350)
						{
							MusicChangerPulse = 350;//10 секунд.
						}
						MusicNumPlaying = mNum;
						MusicVolume = 1.0;
						ACS_NamedExecuteWithResult("Extra_StartMusicCS",mNum,MusicVolume);
						delay(35);
					}
					if(CheckInventory("ExtraMusicScriptRun") == CheckNum)
					{
						if(!ActorIsAlive(0))
						{
							MusicNumPlaying = -1;
							MusicVolume = 1.0;
							ACS_NamedExecuteWithResult("Extra_StopMusicCS",MusicVolume);
							Music_priority = 0;
						}
						SetUserVariable(0,"user_music",0);
						break;
					}
					else
					{
						if(GetCVAR("Extra_debug"))log(s:"Extra_StartMusic CheckNum false");
						SetUserVariable(0,"user_music",0);
						break;
					}
				}
				delay(70);
			}
			TakeInventory("ExtraMusicScriptRun",INT_MAX);
		}
	}
}

Script "Extra_VolumeMusic" (int Volume) CLIENTSIDE//set volume music
{
	if(GetUserCvar(ConsolePlayerNumber(),"extra_localmusic") == 0)
	{
		if(GetUserCvar(ConsolePlayerNumber(),"extra_music") == 1)
		{
			SetMusicVolume(Volume);
		}
		else
		{
			SetMusicVolume(1.0);
		}
	}
}

Script "Extra_StartMusicCS" (int mNum, int Volume) CLIENTSIDE
{
	if(GetUserCvar(ConsolePlayerNumber(),"extra_localmusic") == 0)
	{
		if(GetUserCvar(ConsolePlayerNumber(),"extra_music") == 1)
		{
			SetMusic(StrParam(l:"Prefix_LSDMusicName",i:mNum),0);
			SetMusicVolume(Volume);
		}
		else
		{
			SetMusic("*",0);
			SetMusicVolume(1.0);
		}
	}
}

Script "Extra_StopMusicCS" (int Volume) CLIENTSIDE//set default music
{
	if(GetUserCvar(ConsolePlayerNumber(),"extra_localmusic") == 0)
	{
		SetMusic("*",0);
		SetMusicVolume(Volume);
	}
}
//==============================================================================
Script "Extra_OPEN" OPEN
{
	ACS_NamedExecuteAlways("Extra_CoolDownMusicChanger",0);
}

script "Extra_Enter" ENTER
{
	SetPlayerProperty(0, ON, PROP_NOTARGET);
	delay(5*35);
	SetPlayerProperty(0, OFF, PROP_NOTARGET);
}

script "Extra_Respawn" RESPAWN
{
	SetPlayerProperty(0, ON, PROP_NOTARGET);
	delay(5*35);
	SetPlayerProperty(0, OFF, PROP_NOTARGET);
}
