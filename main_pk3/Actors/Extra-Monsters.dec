Actor ExtraCeilingTurret 32767
{
//$Category Monsters/Extra
//$Sprite TURTA1
//$Title "Ceiling Turret"
Monster
Health 125
Height 12
Radius 20
Mass 10000000
Speed 0
PainChance 0
MinMissileChance 100
MaxTargetRange 2048
-SOLID
-CANPASS
+AMBUSH
+SPAWNCEILING
+CEILINGHUGGER
+NOGRAVITY
+NOBLOOD
+DONTDRAIN
+DONTMORPH
+NOSPLASHALERT
+SEEINVISIBLE
+STANDSTILL
+ALWAYSFAST
+QUICKTORETALIATE
+DONTFALL
+DONTBLAST
+NOTARGET
+NOFEAR
+CANTSEEK
+NOICEDEATH
+NORADIUSDMG
-COUNTKILL
+RANDOMIZE
+DONTHARMSPECIES
+DONTHARMCLASS
Species "SS"
DamageType "SS"
RadiusDamageFactor 0.25
DamageFactor "DrainLife", 0.0
DamageFactor "Poison", 0.0
DamageFactor "PoisonCloud", 0.0
SeeSound "ExtraTurret/See"
ActiveSound "ExtraTurret/Act"
AttackSound ""
DeathSound "weapons/rocklx"
Obituary "%o was shoted by a Ceiling Turret."
Tag "Ceiling Turret"
States
	{
	Spawn:
		TURT A 10
		Goto Idle
	Idle:
		TURT A 5 A_LookEx(LOF_NOSOUNDCHECK,0,2048,2048,360,"See")
		Loop
	See:
		TURT A 2 A_Chase
		Loop
	Missile:
	Pain:
		TNT1 A 0 A_ChangeFlag(FRIENDLY,0)
		TNT1 A 0 A_PlaySound("ExtraTurret/Fire",CHAN_WEAPON)
		TNT1 A 0 A_FaceTarget(180)
		TURT B 4 Bright A_CustomBulletAttack(8,0,1,random(1,5)*3,"Extra_BulletPuff",2048,CBAF_NORANDOM)
		TURT D 3 Bright A_SentinelRefire
		TURT A 4 A_SentinelRefire
		Loop
	Death:
		BALL A 6 Bright A_Scream
		BALL BCDE 6 Bright
		TURT C -1
		Stop
	}
}
//==============================================================================
Actor Extra_TeslaCoil 32766
{
//$Category Monsters/Extra
//$Sprite TESLA0
//$Title "Tesla Coil"
Monster
Health 1000
Height 56
Radius 16
Mass 10000
Speed 0
MaxTargetRange 512
-COUNTKILL
+FLOORCLIP
+NOFEAR
+NOBLOOD
+CANTSEEK
+DONTDRAIN
+DONTMORPH
+NOICEDEATH
+NORADIUSDMG
+SEEINVISIBLE
+STANDSTILL
+LOOKALLAROUND
+RANDOMIZE
+DONTHARMSPECIES
+DONTHARMCLASS
Species "SS"
RadiusDamageFactor 0.25
DamageFactor "Electric", 0.0
DamageFactor "DrainLife", 0.0
DamageFactor "Poison", 0.0
DamageFactor "PoisonCloud", 0.0
DeathSound "weapons/rocklx"
Obituary "%o was zapped by a tesla coil."
Tag "Tesla Coil"
States
	{
	Spawn:
		TESL A 10
		Goto Idle
	Idle:
		TESL A 5 A_LookEx(LOF_NOSOUNDCHECK|LOF_NOSEESOUND ,0,1250,1250,360,"See")
		TNT1 A 0 A_PlaySound("Extra_Tesla/Loop",5,1.0,1)
		Loop
	See:
		TESL AAAAA 1 A_Chase
		TNT1 A 0 A_PlaySound("Extra_Tesla/Loop",5,1.0,1)
		TNT1 A 0 A_ClearTarget
		Loop
	Missile:
		TESL A 9
		TESL C 1 Bright
		TNT1 A 0 A_PlaySound("Extra_Tesla/Attack",CHAN_WEAPON,1.0,false)
		TNT1 A 0 A_SpawnItemEx("Extra_TeslaZapper",0,0,60,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER,0)
		TNT1 A 0 A_AlertMonsters(1250)
		TESL C 10 Bright
		TESL A 49
		Goto See
	Death:
		TESL A 1 Bright A_ScreamAndUnblock
		TNT1 AAA 0 A_StopSound(5)
		TESL A 1 Bright A_SpawnItemEx("ExtraSentryBoom",0,0,60,0,0,0,0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION,0)
		TESL B -1
		Stop
	}
}

Actor Extra_TeslaZapper
{
Height 2
Radius 2
RenderStyle None
ReactionTime 10
+ISMONSTER
-COUNTKILL
+NOTARGET
+DONTBLAST
+NOTELEPORT
+NOBLOCKMONST
+THRUACTORS
+NOINTERACTION
+NOTONAUTOMAP
+DONTHARMSPECIES
+DONTHARMCLASS
Species "SS"
Obituary "%o was zapped by a tesla coil."
Tag "Tesla Coil"
States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
	Missile:
		TNT1 A 0 A_FaceTarget(180,180)
		TNT1 A 0 A_Warp(AAPTR_MASTER,0,0,60,angle,WARPF_ABSOLUTEANGLE|WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION)
		TNT1 A 0 A_CustomRailgun(2,0,None,None,RGF_SILENT|RGF_FULLBRIGHT|RGF_CENTERZ,1,12,"Extra_TeslaZapperPuff",0,0,(ACS_NamedExecuteWithResult("Extra_DistToTarget",0,AAPTR_TARGET,1,1250)*1.0)+2,2,1.0,0.01,"Extra_TeslaZapperTrail",0)
		TNT1 A 1
		TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_TARGET,AAPTR_NULL)==1 || IsPointerEqual(AAPTR_MASTER,AAPTR_NULL)==1,"Death")
		TNT1 A 0 A_CountDown
		TNT1 A 0 A_JumpIfTargetInLOS(1,360,JLOSF_TARGETLOS,1250)
		Goto Death
		TNT1 A 0
		Loop
	Death:
		TNT1 A 1
		TNT1 A 5
		Stop
	}
}

Actor Extra_TeslaZapperTrail
{
Height 0
Radius 0
Scale 0.05
RenderStyle Add
Alpha 0.95
+NOTIMEFREEZE
+NOCLIP
+NOBLOCKMAP
+DONTSPLASH
+THRUACTORS
+NOINTERACTION
+FORCEXYBILLBOARD
+CLIENTSIDEONLY
+NONETID
States
	{
	Spawn:
		TZP1 A 2 Bright
		Stop
	}
}
